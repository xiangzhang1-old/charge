SUBROUTINE WRITEPAV
! THIS FILE IS PART OF THE VASPKIT PACKAGE.
! COPYRIGHT (C) 2014 V. WANG
! THIS FILE IS DISTRIBUTED UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE

! CALCULATE AND WRITE THE PLANAR AVERAGES OF CHARGE DENSITY OR POTENTIAL
  USE MODULE
  IMPLICIT NONE
  INTEGER :: I,J,K
  INTEGER :: NOUT
  REAL*8 :: LENGTH 
  REAL*8, ALLOCATABLE :: GRID(:,:,:)
  WRITE(*,*)
  IF (LEN_TRIM(PARAMS(1)) .EQ. 0) THEN
     WRITE(*,*) 'Which Direction Is For The Planar Average (x, y, or z)?'
     WRITE(*,*)
     WRITE(*,*) 'Your Choice?'
     WRITE(*,*) '------------>>'
     READ(*,*) DIRECTION
     WRITE(*,*) 
  ENDIF
 
  IF (TAG.EQ.41) THEN
     INFILE='CHG'
     CALL READRHO
     ALLOCATE(GRID(NGX,NGY,NGZ))  
!  THE TOTAL CHARGE DENSITY MULTIPLIED BY THE VOLUME IN THE CHGCAR
     GRID=CHGRHO/VOLUME 
  ELSEIF (TAG.EQ.42) THEN
     INFILE='LOCPOT'
     CALL READRHO
     ALLOCATE(GRID(NGX,NGY,NGZ))  
     GRID=ELECTPOT 
  END IF

  IF (DIRECTION.EQ.'x') THEN
     NOUT=NGX
     LENGTH=LATTLEN(1)*SCALING 
  ELSEIF (DIRECTION.EQ.'y') THEN
     NOUT=NGY
     LENGTH=LATTLEN(2)*SCALING 
  ELSEIF (DIRECTION.EQ.'z') THEN
     NOUT=NGZ
     LENGTH=LATTLEN(3)*SCALING
  ENDIF
  
  ALLOCATE(PLANARAVG(NOUT+1))
  ALLOCATE(INTERVAL(NOUT+1))
  PLANARAVG=0.0 
  IF (DIRECTION.EQ.'x') THEN
     DO I=1,NGX
        DO J=1,NGY
           DO  K=1,NGZ
               PLANARAVG(I)=PLANARAVG(I)+GRID(I,J,K)
           END DO
        END DO
       INTERVAL(I)=(I-1)*LENGTH*1.0D0/NOUT
       PLANARAVG(I)=PLANARAVG(I)/FLOAT(NGY*NGZ)
     END DO 
  ELSEIF (DIRECTION.EQ.'y') THEN
     DO I=1,NGY
        DO J=1,NGZ
           DO  K=1,NGX
               PLANARAVG(I)=PLANARAVG(I)+GRID(K,I,J)
           END DO
        END DO
       INTERVAL(I)=(I-1)*LENGTH*1.0D0/NOUT
       PLANARAVG(I)=PLANARAVG(I)/FLOAT(NGX*NGZ)
     END DO 
  ELSEIF (DIRECTION.EQ.'z') THEN
     DO I=1,NGZ
        DO J=1,NGY
           DO  K=1,NGX
               PLANARAVG(I)=PLANARAVG(I)+GRID(K,J,I)
           END DO
        END DO
       INTERVAL(I)=(I-1)*LENGTH*1.0D0/NOUT
       PLANARAVG(I)=PLANARAVG(I)/FLOAT(NGX*NGY)
     END DO 
     ELSE
        WRITE(*,*) 'Wrong Direction: ',DIRECTION
        STOP
  ENDIF
  PLANARAVG(NOUT+1)=PLANARAVG(1) 
  INTERVAL(NOUT+1)=LENGTH
  OPEN(13,FILE='PAVG'//DATSUFF)
  WRITE(13,'(A86)') '# Planar Distance (in Angstrom), Planar Average-Potential (in eV) or -Densitiy (in e)'
    DO I=1,NOUT+1
       WRITE(13,'(F8.4,E18.8)')INTERVAL(I),PLANARAVG(I)
    END DO 
  CLOSE(13)
  PROGRESS='Written PAVG.dat File!'
  CALL READPRT
  DEALLOCATE(GRID)
RETURN
END SUBROUTINE
